# CheckAllCatchTestsFound.cmake.in
# Author: Moritz Beutel

cmake_policy(SET CMP0007 NEW)

set(@PROJECT_NAME@_@TestTarget@_DISCOVERED_CATCH_TESTS "@UnquotedTests@")
list(SORT @PROJECT_NAME@_@TestTarget@_DISCOVERED_CATCH_TESTS)

execute_process(
    COMMAND "${TEST_TARGET_LOCATION}" --list-test-names-only
    OUTPUT_VARIABLE TESTS_OUT
    RESULT_VARIABLE TESTS_RETVAL)
string(REGEX REPLACE "\n" ";" TESTS_OUT "${TESTS_OUT}")
list(SORT TESTS_OUT)
string(REGEX REPLACE "^;" "" TESTS_OUT "${TESTS_OUT}")

if(NOT @PROJECT_NAME@_@TestTarget@_DISCOVERED_CATCH_TESTS STREQUAL TESTS_OUT)
    message("TESTS_OUT: ${TESTS_OUT}")
    message("@PROJECT_NAME@_@TestTarget@_DISCOVERED_CATCH_TESTS: ${@PROJECT_NAME@_@TestTarget@_DISCOVERED_CATCH_TESTS}")
    foreach(TEST IN LISTS TESTS_OUT)
        list(FIND @PROJECT_NAME@_@TestTarget@_DISCOVERED_CATCH_TESTS "${TEST}" IDX)
        if(NOT IDX GREATER -1)
            list(APPEND ADDED_TESTS "${TEST}")
        endif()
    endforeach()
    foreach(TEST IN LISTS @PROJECT_NAME@_@TestTarget@_DISCOVERED_CATCH_TESTS)
        list(FIND TESTS_OUT "${TEST}" IDX)
        if(NOT IDX GREATER -1)
            list(APPEND REMOVED_TESTS "${TEST}")
        endif()
    endforeach()
    
    if(NOT "${ADDED_TESTS}" STREQUAL "")
        string(REPLACE ";" ", " ADDED_TESTS "${ADDED_TESTS}")
        set(ADDED_TESTS "\nTests added: ${ADDED_TESTS}")
    endif()
    if(NOT "${REMOVED_TESTS}" STREQUAL "")
        string(REPLACE ";" ", " REMOVED_TESTS "${REMOVED_TESTS}")
        set(REMOVED_TESTS "\nTests removed: ${REMOVED_TESTS}")
    endif()

    # update a file CMake depends on to trigger a reconfiguration on next build
    file(WRITE "${CMAKE_CURRENT_LIST_DIR}/RediscoverTestsMarker.cmake" "")
  
    message(FATAL_ERROR "tests have been added/removed; build again to discover tests"
        "${ADDED_TESTS}${REMOVED_TESTS}")
endif()
